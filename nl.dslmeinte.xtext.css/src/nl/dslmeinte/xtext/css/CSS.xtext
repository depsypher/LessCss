grammar nl.dslmeinte.xtext.css.CSS with org.eclipse.xtext.common.Terminals hidden(WS, ML_COMMENT)

generate css "http://www.dslmeinte.nl/xtext/css"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


/**
 * Grammar for CSS3 - to be generated partially from a CSS Definition.
 */

CSSFile:
	statements+=Statement*
	;

// re-definition of ID to allow hyphens:
terminal ID:
	('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*
	;

Statement:
	Import | Rule
	;

Import:
	'@import' importURI=STRING
	;

Rule:
	selectors+=SelectorExpression (',' selectors+=SelectorExpression)* '{'
		propertieValues+=PropertyValue (';' propertieValues+=PropertyValue)* ';'?
	'}'
	;

PropertyValue:
	KnownPropertyValue | UnknownPropertyValue
	;

KnownPropertyValue:
	name=KnownProperties ':' values+=ValueLiteral+
	;

UnknownPropertyValue:
	name=ID ':' values+=ValueLiteral+
	;

enum KnownProperties:
	color | border_top='border-top' // | ..
	;
	// TODO  generate into this grammar from other DSL...


/*
 * +----------------------+
 * | Selector expressions |
 * +----------------------+
 * 
 * From high to low precedence:
 * 
 * - universal, type, ID, class: atomic (primary)
 * - ID, class: postfix, left-associative
 * - pseudo-class: postfix, non-associative
 * - child, adjacent sibling, descendant: infix/binary, left-associative
 */

SelectorExpression:
	FamilyExpression
	;

FamilyExpression returns SelectorExpression:
	PseudoClassExpression (
		  {DescendantSelector.left=current}				right=PseudoClassExpression
		| {ChildSelector.left=current} '>'				right=PseudoClassExpression
		| {AdjacentSiblingSelector.left=current} '+'	right=PseudoClassExpression
	)*
	;

PseudoClassExpression returns SelectorExpression hidden():
	PostfixExpression ({PseudoClassSelector.operand=current} ':' pseudoClass=PseudoClasses)?
	;

enum PseudoClasses:
	  active | after | before
	| firstChild='first-child' | firstLetter='first-letter' | firstLine='first-line'
	| focus | hover | lang | link | visited
	;

PostfixExpression returns SelectorExpression hidden():
	PrimarySelectorExpression (
		  {IDSubSelector.parent=current} '#' name=ID
		| {ClassSubSelector.parent=current} '.' name=ID
	)*
	;

PrimarySelectorExpression returns SelectorExpression:
	ToplevelSelector
	;

ToplevelSelector:
	  {UniversalSelector} '*'
	| {TypeSelector} type=HtmlElements
	| {IDSelector} '#' name=ID
	| {ClassSelector} '.' name=ID
	;

enum HtmlElements:
	a | pre | body
	;
	// TODO  generate into this grammar from other DSL...


/*
 * +---------------------+
 * | CSS property values |
 * +---------------------+
 */

ValueLiteral:
	NumberLiteral | StringLiteral | ColorLiteral | BareWordLiteral
	;

NumberLiteral hidden():
	(int=INT | decimal=DECIMAL) dimension=Dimensions?
	;

terminal INT returns ecore::EInt: ('-')? ('0'..'9')+;
terminal DECIMAL returns ecore::EDouble: ('-')? ('0'..'9')+ '.' ('0'..'9')+;

enum Dimensions:
	in | cm | mm | pt | pc | em | ex | px | perc='%'
	;

StringLiteral:
	value=STRING
	;

ColorLiteral:
	  {RGBColor} rgb=RGB_COLOR
	| {NamedColor} color=ColorNames
	| ComponentColorLiteral
	;

ComponentColorLiteral:
	  {ComponentRGBColor} 'rgb' '(' red=NumberLiteral ',' green=NumberLiteral ',' blue=NumberLiteral ')'
	| {ComponentHSLColor} 'hsl' '(' hue=NumberLiteral ',' saturation=NumberLiteral ',' lightness=NumberLiteral ')'
	;
	// validation on ComponentRGBColor:
	//	either all percentages (with correct range), or all dimension-less integers 0-255

enum ColorNames:
	black | white // | ...
	;
	// TODO  generate into this grammar from other DSL

terminal RGB_COLOR:
	'#' ('0'..'9' | 'a'..'f' | 'A'..'F')+;
	// validation:

// something we don't know about:
BareWordLiteral:
	bareWord=ID
	;

// TODO  UriLiteral

