grammar nl.dslmeinte.xtext.less.Less with nl.dslmeinte.xtext.css.CSS hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate less "http://www.dslmeinte.nl/xtext/less/Less"


LessFile:
	statements+=Statement*;

Statement:
	VariableDefinition | RuleSet | VisibleComment | Import
	;

VariableDefinition:
	name=VariableDeclaration ':' value=ValueExpression ';'
	;

VisibleComment:
	comment=ML_COMMENT
	;

Import:
	'@import' importURI=STRING
	;
	// TODO  tweak ImportUriGlobalScopeProvider to check on the importURI's extension and behave accordingly

terminal ESCAPED_VALUE: '~' -> ( ';' | '}' );

terminal JAVASCRIPT_EVAL: '`' -> '`';

/*
 * For now: interpolate STRINGs during generation but don't support the
 * syntax on a grammar level yet. (TODO)
 */


RuleSet:
	selectors+=SelectorExpression (',' selectors+=SelectorExpression)*
	( '(' ( parameters+=Parameter (',' parameters+=Parameter )* )? ')' )?
	'{'
		members+=RuleSetMember*
	'}'
	;

Parameter:
	name=VariableDeclaration (':' defaultValue=ValueExpression)?
	;

VariableDeclaration hidden():
	'@' ( name=ID | name='color' )	// add CSS keywords...
	;

// TODO  1) value converter, 2) use it as a datatype rule...
VariableName hidden():
	'@' ( ID | 'color' )
	;

// TODO  do the same for # and .

// re-def. of PrimarySelectorExpression from CSS to incorporate &-combinator
PrimarySelectorExpression returns SelectorExpression:
	  {ToplevelSelectorIndirection} selector=ToplevelSelector
	| {ParentCombinatorSelector} '&'
	;

RuleSetMember:
	RuleSet | MixinCall | PropertyExpression | VariableDefinition
	;

MixinCall hidden():
	'.' ruleSet=[RuleSet]
		( '(' arguments+=ValueExpression (',' arguments+=ValueExpression)* ')' )?
	';'
	;
	// scoping of ruleSet: all mixin candidates
	// TODO  WS must be hidden beyond <.ID>

PropertyExpression:
	KnownPropertyExpression | UnknownPropertyExpression
	;

KnownPropertyExpression:
	name=KnownProperties ':' values+=ValueExpression+ ';'
	;
	// TODO  make ;'s optional (separating, not delimiting)

UnknownPropertyExpression:
	name=ID ':' values+=ValueExpression+ ';'
	;
	// TODO  make ;'s optional (separating, not delimiting)


/*
 * +-------------------+
 * | Value expressions |
 * +-------------------+
 */

ValueExpression:
	AdditiveExpression
	;

AdditiveExpression returns ValueExpression:
	MultiplicativeExpression ({AdditiveExpression.left=current} operator=AdditiveOperators right=MultiplicativeExpression)*
	;

enum AdditiveOperators:
	plus='+' | minus='-'
	;

MultiplicativeExpression returns ValueExpression:
	ColorFunctionExpression ({MultiplicativeExpression.left=current} operator=MultiplicativeOperators right=ColorFunctionExpression)*
	;

enum MultiplicativeOperators:
	mult='*' | div='/'
	;
	// note that a '%' modulo operator would cause non-LL(*) behavior in combination with the % dimension in (css::)NumberLiteral

ColorFunctionExpression returns ValueExpression:
	  PrimaryValueExpression
	| {ColorFunctionCall} function=ColorFunctions '(' color=ValueExpression ',' modifier=NumberLiteral ')'
	;
	// TODO  validation

enum ColorFunctions:
	  lighten | darken
	| saturate | desaturate
	| fadein | fadeout
	| spin
	| hue | saturation | lightness
	;

PrimaryValueExpression returns ValueExpression:
	  '(' ValueExpression ')'
	| VariableReference
	| {ArgumentsReference} '@arguments'
	| {CSSLiteral}			value=ValueLiteral
	| {EscapedLiteral}		value=ESCAPED_VALUE
	| {JavascriptLiteral}	value=JAVASCRIPT_EVAL
	;

VariableReference hidden():
	(indirections+='@')+ variable=[VariableDeclaration]
	;
	// indirections counts the number of @'s, at least one
	// validation:
	//   e.g., in @@var, the value of @var must be an existing variable
	// scoping:
	//   - mimic nesting of rule sets
	//   - add an implicit @arguments variable...

// re-def. from CSS grammar to make it possible to use expressions:
ComponentColorLiteral:
	  {ComponentRGBColor} 'rgb' '(' red=ValueExpression ',' green=ValueExpression ',' blue=ValueExpression ')'
	| {ComponentHSLColor} 'hsl' '(' hue=ValueExpression ',' saturation=ValueExpression ',' lightness=ValueExpression ')'
	;
